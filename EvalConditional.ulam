local typedef EvalSearch ES;
local typedef EvalStatefulConditional ESC;
local typedef STypeUtils STU;

/**
   Helper for evaluating conditional statements, e.g.

   (if-zero            ; if `reg1` register value is 0
     (do-stuff)
     (do-other-stuff))

   Some contitionals may require multiple retries. In this case
   Exec is set to a corresponding state and condition will be
   later evaluated by EvalStatefulConditional.
 */
transient EvalConditional : Eval {
  constant Symbol cIF                         = 0x1;
  constant Symbol cIF_ZERO                    = 0x2;
  constant Symbol cIF_NOT_ZERO                = 0x3;

  constant Symbol cIF_ATTACHED                = 0x9;
  constant Symbol cIF_ATTACHED_NEXT           = 0xa;
  constant Symbol cIF_ATTACHED_NEXT_2         = 0xb;
  constant Symbol cIF_OTHER_MATCHES_NEXT      = 0xc;
  constant Symbol cIF_OTHER_NEXT_MATCHES_NEXT = 0xd;

  constant Symbol cIF_MATCHES_FLAGS           = 0x11;

  constant Symbol cIF_ONE_IN                  = 0x21;
  constant Symbol cIF_ONE_IN_FACT             = 0x22;

  constant Bool cDEBUG_COND = true;

  Arity getArity(Symbol symbol) {
    return 2;
  }

  Arity getArgNum(Symbol symbol) {
    if (symbol == cIF) {
      return 1;
    }

    return 0;
  }

  Arity eval(Exec& exec, Symbol symbol, Byte arg) {
    DebugUtils du;
    Bool cond = false;

    if (symbol == cIF) {
      if (exec.mDebug) dbg.print("(if)");
      cond = (arg != 0x0);

    } else if (symbol == cIF_ZERO) {
      if (exec.mDebug) dbg.print("(if-zero)");
      cond = (exec.getInteger(Exec.cREG1) == 0);

    } else if (symbol == cIF_NOT_ZERO) {
      if (exec.mDebug) dbg.print("(if-not-zero)");
      cond = (exec.getInteger(Exec.cREG1) != 0);

    } else if (symbol == cIF_ATTACHED) {
      if (exec.mDebug) dbg.print("(if-attached)");
      cond = exec.getBond(getBondIndex(exec, cSRC)).isAttached();

    } else if (symbol == cIF_ATTACHED_NEXT) {
      if (exec.mDebug) dbg.print("(if-attached-next)");
      cond = isAttachedNext(exec);

    } else if (symbol == cIF_ATTACHED_NEXT_2) {
      if (exec.mDebug) dbg.print("(if-attached-next-2)");
      exec.setState(ESC.cSTATE_IF_ATTACHED_NEXT_2);

    } else if (symbol == cIF_OTHER_MATCHES_NEXT) {
      if (exec.mDebug) dbg.print("(if-other-matches-next)");
      cond = isOtherMatchingNext(exec);

    } else if (symbol == cIF_OTHER_NEXT_MATCHES_NEXT) {
      if (exec.mDebug) dbg.print("(if-other-next-matches-next)");
      cond = isOtherNextMatchingNext(exec);

    } else if (symbol == cIF_MATCHES_FLAGS) {
      if (exec.mDebug) dbg.print("(if-matches-flags)");
      cond = isMatchingFlags(exec);

    } else if (symbol == cIF_ONE_IN) {
      if (exec.mDebug) dbg.print("(if-one-in)");
      Random random;
      cond = random.oneIn((Unsigned) exec.getInteger(cREG1));

    } else if (symbol == cIF_ONE_IN_FACT) {
      if (exec.mDebug) dbg.print("(if-one-in-fact)");
      Random random;
      cond = random.oneIn((Unsigned) (exec.getInteger(cREG1) * exec.getInteger(cREG2)));

    } else {
      dbg.print("EvalConditional.eval: unknown symbol");
      dbg.print((Unsigned) symbol);
    }

    if (exec.mDebug && cDEBUG_COND) dbg.print(cond ? "  yes" : "  no");

    if (getArgNum(symbol) == 1)
      return cond ? 1u : Arity.maxof;
    return cond ? 0u : 1u;
  }

  Bool isAttachedNext(Exec& exec) {
    Index index = getBondIndex(exec, cSRC);
    Index nextIndex = getBondIndex(exec, cDST);

    QBond& bond = exec.getBond(index);
    if (!bond.isAttached())
      return false;

    IBondable& bondable = (IBondable&) ew[bond.getSiteNumber()];
    return bondable.getBond(nextIndex).isAttached();
  }

  Bool isOtherMatchingNext(Exec& exec) {
    Index index = getBondIndex(exec, cSRC);
    Index nextIndex = getBondIndex(exec, cDST);
    Index otherIndex = (index == Exec.cINPUT) ? Exec.cOUTPUT : Exec.cINPUT;

    QBond& bond = exec.getBond(index);
    QBond& otherBond = exec.getBond(otherIndex);
    if (!bond.isAttached() || !otherBond.isAttached())
      return false;

    SiteNum site = bond.getSiteNumber();
    IBondable& bondable = (IBondable&) ew[site];
    QBond& nextBond = bondable.getBond(nextIndex);
    if (!nextBond.isAttached())
      return false;

    return (otherBond.getSiteNumber() == nextBond.getSiteNumber(site));
  }

  Bool isOtherNextMatchingNext(Exec& exec) {
    Index index = getBondIndex(exec, cSRC);
    Index nextIndex = getBondIndex(exec, cDST);
    Index otherIndex = (index == Exec.cINPUT) ? Exec.cOUTPUT : Exec.cINPUT;
    Index otherNextIndex = getBondIndex(exec, cREG1);

    QBond& bond = exec.getBond(index);
    QBond& otherBond = exec.getBond(otherIndex);
    if (!bond.isAttached() || !otherBond.isAttached())
      return false;

    SiteNum site = bond.getSiteNumber();
    IBondable& bondable = (IBondable&) ew[site];
    QBond& nextBond = bondable.getBond(nextIndex);
    if (!nextBond.isAttached())
      return false;

    SiteNum otherSite = otherBond.getSiteNumber();
    IBondable& otherBondable = (IBondable&) ew[otherSite];
    QBond& otherNextBond = otherBondable.getBond(otherNextIndex);
    if (!otherNextBond.isAttached())
      return false;

    return otherNextBond.getSiteNumber(otherSite) == nextBond.getSiteNumber(site);
  }

  Bool isMatchingFlags(Exec& exec) {
    Index index = getBondIndex(exec, cSRC);
    Byte flags = exec.get(cREG1);

    QBond& bond = exec.getBond(index);
    if (!bond.isAttached())
      return false;

    EvalSearch es;
    SiteNum site = bond.getSiteNumber();
    return es.match(site, flags);
  }
}
