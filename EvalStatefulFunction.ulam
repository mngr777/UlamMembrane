local typedef BondStatus BS;
local typedef BondUtils BU;

/**
   Helper for evaluating functions that may require multiple retries.
 */
transient EvalStatefulFunction : Eval {
  typedef Exec.State State;

  constant State cSTATE_ATTACH_OTHER_TO_NEXT = 33;
  constant State cSTATE_CONNECT_OTHER_FORCE  = 34;
  constant State cSTATE_TRAVERSE             = 35;
  constant State cSTATE_BUILD                = 36;
  constant State cSTATE_MOVE_TO_OTHER        = 37;
  constant State cSTATE_MOVE_TO_OTHER_NEXT   = 38;
  constant State cSTATE_INSERT_SEQ           = 39;

  constant State cSTATE_FIRST = cSTATE_ATTACH_OTHER_TO_NEXT;
  constant State cSTATE_LAST  = cSTATE_INSERT_SEQ;

  Byte eval(Exec& exec, Package& tree, Bool& done) {
    if (exec.isState(cSTATE_ATTACH_OTHER_TO_NEXT)) {
      return attachOtherToNext(exec, done);

    } else if (exec.isState(cSTATE_CONNECT_OTHER_FORCE)) {
      return connectOtherForce(exec, done);

    } else if (exec.isState(cSTATE_TRAVERSE)) {
      return traverse(exec, done);

    } else if (exec.isState(cSTATE_BUILD)) {
      return build(exec, done);

    } else if (exec.isState(cSTATE_MOVE_TO_OTHER)) {
      return moveToOther(exec, done);

    } else if (exec.isState(cSTATE_MOVE_TO_OTHER_NEXT)) {
      return moveToOtherNext(exec, done);

    } else if (exec.isState(cSTATE_INSERT_SEQ)) {
      return insertSeq(exec, done);
    }

    return 0x0;
  }

  Bool attachOtherToNext(Exec& exec, Bool& done) {
    Index index = getBondIndex(exec, cSRC);
    Index nextIndex = getBondIndex(exec,  cDST);
    Index otherIndex = (index == Exec.cINPUT) ? Exec.cOUTPUT : Exec.cINPUT;
    done = false;

    QBond& bond = exec.getBond(index);
    if (!bond.isAttached()) {
      // Nothing to attach to
      done = true;
      return false;
    }

    SiteNum site = bond.getSiteNumber();
    IBondable& bondable = (IBondable&) ew[site];
    QBond& nextBond = bondable.getBond(nextIndex);
    if (!nextBond.isAttached()) {
      // Still nothing to attach to (no next atom)
      done = true;
      return false;
    }

    SiteNum nextSite = nextBond.getSiteNumber(site);
    if (!ew.isAccessible(nextSite))
      return false;

    IBondable& next = (IBondable&) ew[nextSite];
    BS.Status status = bu.attach(0, otherIndex, nextSite, BU.cCOMMON);
    done = (status != BS.cINACCESSIBLE && status != BS.cDISTANCE_EXCEEDS_MAX);
    return bs.isOk(status);
  }

  Bool connectOtherForce(Exec& exec, Bool& done) {
    Index index = getBondIndex(exec, cSRC);
    Index connectIndex = getBondIndex(exec, cDST);
    Index otherIndex = (index == Exec.cINPUT) ? Exec.cOUTPUT : Exec.cINPUT;
    Index connectOtherIndex = getBondIndex(exec, cREG1);
    done = false;

    QBond& bond = exec.getBond(index);
    QBond& otherBond = exec.getBond(otherIndex);
    if (!bond.isAttached() || !otherBond.isAttached()) {
      done = true; // one or both atoms are missing
      return false;
    }

    SiteNum site = bond.getSiteNumber();
    SiteNum otherSite = otherBond.getSiteNumber();
    BS.Status status = bu.attachForce(site, connectIndex, otherSite, connectOtherIndex);
    done = (status != BS.cINACCESSIBLE && status != BS.cDISTANCE_EXCEEDS_MAX);
    return bs.isOk(status);
  }

  Bool traverse(Exec& exec, Bool& done) {
    Index index = getBondIndex(exec, cSRC);
    Index indexNext = getBondIndex(exec, cDST);

    BS.Status status = bu.traverse(0, index, indexNext);
    done = (status != BS.cINACCESSIBLE);
    return bs.isOk(status);
  }

  Bool build(Exec& exec, Bool& done) {
    SType stype = getSType(exec, cREG1);
    Index index = getBondIndex(exec, cSRC);
    Index buildIndex = getBondIndex(exec, cDST);
    done = false;

    if (exec.getBond(index).isAttached()) {
      // Already has an atom attached
      done = true;
      return false;
    }

    // Make new atom
    Atom atom = stu.make(stype);
    IBondable& bondable = (IBondable&) atom;

    // Place new atom
    Radius radius = bu.maxDist(bondable, exec);
    EventWindowMisc ewm;
    SiteNum buildSite = ewm.findEmptySite(1, radius);
    if (buildSite == SiteNum.maxof)
      return false;
    ew[buildSite] = atom;

    // Attach
    BS.Status status = bu.attach(0, index, buildSite, buildIndex);
    if (!bs.isOk(status)) {
      Empty empty;
      ew[buildSite] = empty;

      if (status == BS.cINACCESSIBLE || status == BS.cDISTANCE_EXCEEDS_MAX)
        return false;
    }

    done = true;
    return bs.isOk(status);
  }

  Bool moveToOther(Exec& exec, Bool& done) {
    Index index = getBondIndex(exec, cSRC);
    Index toIndex = getBondIndex(exec, cDST);
    Index otherIndex = (index == Exec.cOUTPUT) ? Exec.cINPUT : Exec.cOUTPUT;
    done = false;

    QBond& bond = exec.getBond(index);
    QBond& otherBond = exec.getBond(otherIndex);
    if (!bond.isAttached() || !otherBond.isAttached()) {
      // Nothing to attach and/or nothing to attach to
      done = true;
      return false;
    }

    SiteNum site = bond.getSiteNumber();
    SiteNum otherSite = otherBond.getSiteNumber();

    BS.Status status = bu.move(site, bond.getIndex(), otherSite, toIndex);
    done = (status != BS.cINACCESSIBLE && status != BS.cDISTANCE_EXCEEDS_MAX);
    return bs.isOk(status);
  }

  Bool moveToOtherNext(Exec& exec, Bool& done) {
    Index index = getBondIndex(exec, cSRC);
    Index nextIndex = getBondIndex(exec, cDST);
    Index toIndex = getBondIndex(exec, cREG1);
    Index otherIndex = (index == Exec.cOUTPUT) ? Exec.cINPUT : Exec.cOUTPUT;
    done = false;

    QBond& bond = exec.getBond(index);
    QBond& otherBond = exec.getBond(otherIndex);
    if (!bond.isAttached() || !otherBond.isAttached()) {
      done = true;
      return false;
    }
    SiteNum site = bond.getSiteNumber();
    SiteNum otherSite = otherBond.getSiteNumber();

    // Atom attached to other bond
    IBondable& other = (IBondable&) ew[otherSite];
    QBond& nextBond = other.getBond(nextIndex);
    if (!nextBond.isAttached()) {
      done = true;
      return false;
    }

    // Next atom
    SiteNum nextSite = nextBond.getSiteNumber(otherSite);
    if (!ew.isAccessible(nextSite))
      return false;

    // Move
    BS.Status status = bu.move(site, bond.getIndex(), nextSite, toIndex);
    if (status == BS.cINACCESSIBLE || status == BS.cDISTANCE_EXCEEDS_MAX)
      return false;

    done = true;
    return bs.isOk(status);
  }

  Bool insertSeq(Exec& exec, Bool& done) {
    Index index = getBondIndex(exec, cSRC);
    Index fromIndex = getBondIndex(exec, cDST);
    done = false;

    // Get first bondable
    QBond& prevBond = exec.getBond(index);
    if (!prevBond.isAttached()) {
      // First atom not attached
      done = true;
      return false;
    }
    SiteNum prevSite = prevBond.getSiteNumber();
    IBondable& prevBondable = (IBondable&) ew[prevSite];

    // Make new atom
    Atom atom = stu.make(stu.getSType(prevSite));
    IBondable& bondable = (IBondable&) atom;

    // Place new atom near the first one,
    // hope it's close enough to the second
    Radius radius = bu.maxDist(bondable, prevBondable);
    EventWindowMisc ewm;
    SiteNum site = ewm.findEmptySiteAround(prevSite, 1, radius);
    if (site == SiteNum.maxof)
      return false;
    ew[site] = atom;

    // Insert
    BS.Status status = bu.insertSeq(prevSite, fromIndex, site);
    if (!bs.isOk(status)) {
      Empty empty;
      ew[site] = empty;

      if (status == BS.cINACCESSIBLE || status == BS.cDISTANCE_EXCEEDS_MAX)
        return false;
    }

    done = true;
    return bs.isOk(status);
  }
}
