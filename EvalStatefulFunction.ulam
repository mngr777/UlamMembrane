local typedef BondStatus BS;
local typedef BondUtils BU;

transient EvalStatefulFunction : Eval {
  typedef Exec.State State;

  constant State cSTATE_ATTACH_OTHER_TO_NEXT = 33;
  constant State cSTATE_CONNECT_OTHER_FORCE  = 34;
  constant State cSTATE_TRAVERSE             = 35;
  constant State cSTATE_BUILD                = 36;
  constant State cSTATE_BUILD_NEXT           = 37;
  constant State cSTATE_MOVE_TO_OTHER        = 38;

  constant State cSTATE_FIRST = cSTATE_ATTACH_OTHER_TO_NEXT;
  constant State cSTATE_LAST  = cSTATE_MOVE_TO_OTHER;

  Byte eval(Exec& exec, Package& tree, Bool& done) {
    if (exec.isState(cSTATE_ATTACH_OTHER_TO_NEXT)) {
      return attachOtherToNext(exec, done);

    } else if (exec.isState(cSTATE_CONNECT_OTHER_FORCE)) {
      return connectOtherForce(exec, done);

    } else if (exec.isState(cSTATE_TRAVERSE)) {
      done = traverse(exec);

    } else if (exec.isState(cSTATE_BUILD)) {
      done = build(exec);

    } else if (exec.isState(cSTATE_BUILD_NEXT)) {
      return buildNext(exec, done);

    } else if (exec.isState(cSTATE_MOVE_TO_OTHER)) {
      done = moveToOther(exec);
    }

    return 0x0;
  }

  Bool attachOtherToNext(Exec& exec, Bool& done) {
    Index index = getBondIndex(exec, cSRC);
    Index nextIndex = getBondIndex(exec,  cDST);
    Index otherIndex = (index == Exec.cINPUT) ? Exec.cOUTPUT : Exec.cINPUT;
    done = false;

    QBond& bond = exec.getBond(index);
    if (!bond.isAttached()) {
      // Nothing to attach to
      done = true;
      return false;
    }

    SiteNum site = bond.getSiteNumber();
    IBondable& bondable = (IBondable&) ew[site];
    QBond& nextBond = bondable.getBond(nextIndex);
    if (!nextBond.isAttached()) {
      // Still nothing to attach to
      done = true;
      return false;
    }

    SiteNum nextSite = nextBond.getSiteNumber(site);
    if (!ew.isAccessible(nextSite))
      return false;

    IBondable& next = (IBondable&) ew[nextSite];
    BS.Status status = bu.attach(0, otherIndex, nextSite, BU.cCOMMON);
    done = (status != BS.cINACCESSIBLE && status != BS.cDISTANCE_EXCEEDS_MAX);
    return bs.isOk(status);
  }

  Bool connectOtherForce(Exec& exec, Bool& done) {
    Index index = getBondIndex(exec, cSRC);
    Index connectIndex = getBondIndex(exec, cDST);
    Index otherIndex = (index == Exec.cINPUT) ? Exec.cOUTPUT : Exec.cINPUT;
    Index connectOtherIndex = getBondIndex(exec, cREG1);
    done = false;

    QBond& bond = exec.getBond(index);
    QBond& otherBond = exec.getBond(otherIndex);
    if (!bond.isAttached() || !otherBond.isAttached()) {
      done = true; // one or both atoms are missing
      return false;
    }

    SiteNum site = bond.getSiteNumber();
    SiteNum otherSite = otherBond.getSiteNumber();
    BS.Status status = bu.attachForce(site, connectIndex, otherSite, connectOtherIndex);
    done = (status != BS.cDISTANCE_EXCEEDS_MAX);
    return bs.isOk(status);
  }

  // TODO: return and set done value
  Bool traverse(Exec& exec) {
    Index index = getBondIndex(exec, cSRC);
    Index indexNext = getBondIndex(exec, cDST);
    BS.Status status = bu.traverse(0, index, indexNext);
    return (status != BS.cINACCESSIBLE);
  }

  // TODO: return and set done value
  Bool build(Exec& exec) {
    SType stype = getSType(exec, cREG1);
    Index index = getBondIndex(exec, cSRC);
    Index buildIndex = getBondIndex(exec, cDST);

    if (exec.getBond(index).isAttached())
      return true; // already have an atom attached

    // Make new atom
    Atom atom = stu.make(stype);
    IBondable& bondable = (IBondable&) atom;

    // Place new atom
    Radius radius = bu.maxDist(bondable, exec);
    EventWindowMisc ewm;
    SiteNum buildSite = ewm.findEmptySite(1, radius);
    if (buildSite == SiteNum.maxof)
      return false;
    ew[buildSite] = atom;

    // Attach
    BS.Status status = bu.attach(0, index, buildSite, buildIndex);
    if (!bs.isOk(status)) {
      Empty empty;
      ew[buildSite] = empty;
    }
    return (status != BS.cINACCESSIBLE);
  }

  Bool buildNext(Exec& exec, Bool& done) {
    Index index = getBondIndex(exec, cSRC);
    Index nextIndex = getBondIndex(exec, cDST);
    SType stype = getSType(exec, cREG1);
    Index bondIndex = getBondIndex(exec, cREG2);
    done = false;

    // Get currently attached bondable
    QBond& bond = exec.getBond(index);
    if (!bond.isAttached()) {
      // nothing attached to selected bond
      done = true;
      return false;
    }
    SiteNum site = bond.getSiteNumber();
    IBondable& current = (IBondable&) ew[site];

    // Check target bond
    QBond& nextBond = current.getBond(nextIndex);
    if (nextBond.isAttached()) {
      // something is already attached to the target bond
      done = true;
      return false;
    }

    // Make new atom
    Atom atom = stu.make(stype);
    IBondable& next = (IBondable&) atom;

    // Place new atom
    Radius radius = bu.maxDist(next, current);
    EventWindowMisc ewm;
    SiteNum nextSite = ewm.findEmptySiteAround(site, 1, radius);
    if (nextSite == SiteNum.maxof)
      return false;
    ew[nextSite] = atom;

    // Attach to current
    BS.Status status = bu.attach(site, nextIndex, nextSite, bondIndex);
    if (!bs.isOk(status)) {
      Empty empty;
      ew[nextSite] = empty;
    }
    done = (status != BS.cINACCESSIBLE);
    return bs.isOk(status);
  }

  // TODO: return and set done value
  Bool moveToOther(Exec& exec) {
    Index index = getBondIndex(exec, cSRC);
    Index indexTo = getBondIndex(exec, cDST);
    Index indexOther = (index == Exec.cOUTPUT) ? Exec.cINPUT : Exec.cOUTPUT;

    QBond& bond = exec.getBond(index);
    QBond& bondOther = exec.getBond(indexOther);
    if (!bond.isAttached() || !bondOther.isAttached())
      return true;

    SiteNum site = bond.getSiteNumber();
    SiteNum siteOther = bondOther.getSiteNumber();
    if (!ew.isAccessible(site) || !ew.isAccessible(siteOther))
      return false;

    BS.Status status = bu.move(site, bond.getIndex(), siteOther, indexTo);
    if (status == BS.cINACCESSIBLE || status == BS.cDISTANCE_EXCEEDS_MAX)
      return false;

    return true;
  }
}
