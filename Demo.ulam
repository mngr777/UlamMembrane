local typedef BondStatus BS;
local typedef EventWindow.SiteNum SiteNum;
local typedef PackageData PD;
local typedef STypeUtils STU;

element Demo : QStateT(3) + QBondableT(1) + QMortal {
  constant State cPLACE_MEMBRANE_BUILDER = 0;
  constant State cBUILD_SEQUENCE         = 1;

  Unsigned mCount = 0;

  @Override Void behave() {
    if (isState(cPLACE_MEMBRANE_BUILDER)) {
      placeMembraneBuilder();
    } else if (isState(cBUILD_SEQUENCE)) {
      buildSequence();
    } else if (isState(cSTATE_DONE)) {
      die();
    }
  }

  Void placeMembraneBuilder() {
    EventWindow ew;

    Demo_MembraneBuilder builder;
    ew[1] = builder;

    setState(cBUILD_SEQUENCE);
  }

  Void buildSequence() {
    Demo_SequenceData data;

    SequenceBuilder builder;
    if (builder.buildNext(self, 0, data.getData(mCount), data.getAux(mCount)))
      mCount++;

    if (mCount >= data.cSIZE)
      setState(cSTATE_DONE);
  }
}


/**
   \symbol DT
 */
element Demo_TreeBuilder : QTreeBuilder + QBondableT(2) + QDiffusableT(100) + QMortal {
  constant State cSTATE_DETACH_SEQUENCE = cSTATE_BUILD_DONE;
  constant State cSTATE_ATTACH_EXEC     = cSTATE_BUILD_DONE + 1;

  virtual IBondable& getBondable() {
    return self;
  }

  @Override Void behave() {
    step();

    if (isState(cSTATE_DONE)) {
      die();
    } else {
      diffuse();
    }
  }

  Void step() {
    if (isState(cSTATE_DETACH_SEQUENCE)) {
      detachSequence();

    } else if (isState(cSTATE_ATTACH_EXEC)) {
      attachExec();

    } else {
      self.QTreeBuilder.step();
    }
  }

  Void detachSequence() {
    getBond(cSEQUENCE).detach();
    setState(cSTATE_ATTACH_EXEC);
  }

  Void attachExec() {
    BondStatus bs;
    BondUtils bu;
    EventWindow ew;
    EventWindowMisc ewm;

    QBond& bond = getBond(cTREE);
    SiteNum treeSite = bond.getSiteNumber();
    Package& tree = (Package&) ew[treeSite];

    // Build exec
    Exec exec;
    SiteNum site = ewm.findEmptySiteAround(treeSite, 1u, bu.maxDist(exec, tree));
    if (site == SiteNum.maxof)
      return;
    ew[site] = exec;

    // Replace ourselves with exec
    BS.Status status = bu.replace(treeSite, Package.cCOMMON, site, Exec.cPROGRAM);
    if (!bs.isOk(status)) {
      Empty empty;
      ew[site] = empty;
      return;
    }

    setState(cSTATE_DONE);
  }
}


local typedef QBondableT(1) QBondable1;

/**
   \symbol DM
 */
element Demo_MembraneBuilder : QStateT(3) + QBondableT(1) + QDiffusableT(100) + QMortal {
  typedef C2D.Dir Dir;

  constant Unsigned(5) cSIDE_LENGTH = 16;
  constant Unsigned(2) cSTEP_LENGTH = 2;

  constant State cSTATE_MOVE_OUT       = 0;
  constant State cSTATE_BUILD          = 1;
  constant State cSTATE_ANCHOR         = 2;
  constant State cSTATE_TRAVERSE       = 3;
  constant State cSTATE_FORWARD        = 4;
  constant State cSTATE_CLOSE          = 5;
  constant State cSTATE_REMOVE_ANCHORS = 6;

  Dir mDir = 0; // South
  Unsigned(5) mCount = 0;
  Unsigned(2) mMoveCount = 0;

  @Override Void behave() {
    if (isState(cSTATE_MOVE_OUT)) {
      moveOut();
    } else if (isState(cSTATE_BUILD)) {
      build();
    } else if (isState(cSTATE_ANCHOR)) {
      anchor();
    } else if (isState(cSTATE_TRAVERSE)) {
      traverse();
    } else if (isState(cSTATE_FORWARD)) {
      forward();
    } else if (isState(cSTATE_CLOSE)) {
      close();
    } else if (isState(cSTATE_REMOVE_ANCHORS)) {
      removeAnchors();
      diffuse();
    } else if (isState(cSTATE_DONE)) {
      die();
    }
  }

  Void moveOut() {
    // NOTE: cannot do checks after swapping
    if (mCount >= cSIDE_LENGTH * cSTEP_LENGTH / 2) {
      mCount = 0;
      setState(cSTATE_BUILD);
      return;
    }

    EventWindow ew;
    SwapHelper sh;
    C2D origin;
    C2D coord = origin.neighbor(5); // North-West
    if (ew.isEmpty(coord)) {
      mCount++; // increment _before_ swapping
      if (!sh.swap(origin, coord))
        mCount--;
    }
  }

  Void build() {
    BondStatus bs;
    BondUtils bu;
    EventWindow ew;
    EventWindowMisc ewm;

    // Build
    SiteNum site = ewm.findEmptySite(1, 2);
    if (site == SiteNum.maxof)
      return;
    Membrane membrane;
    ew[site] = membrane;

    BS.Status status = BS.cOK;
    QBond& bond = getBond(0);
    if (!bond.isAttached()) {
      // First membrane atom, attach to self
      status = bu.attach(0, 0, site, Membrane.cCOMMON);
    } else {
      // Attach to previous membrane atom
      status = bu.attach(bond.getSiteNumber(), Membrane.cNEXT, site, Membrane.cPREV);
    }

    if (bs.isOk(status)) {
      setState(cSTATE_ANCHOR);

    } else {
      Empty empty;
      ew[site] = empty;
      return;
    }
  }

  Void anchor() {
    BondStatus bs;
    BondUtils bu;
    CoordUtils cu;
    EventWindow ew;
    EventWindowMisc ewm;

    QBond& bond = getBond(0);
    SiteNum membraneSite = bond.getSiteNumber();
    Membrane& membrane = (Membrane&) ew[membraneSite];
    QBond& otherBond = membrane.getBond(Membrane.cOTHER);

    if (mCount > 0 || otherBond.isAttached()) {
      setState(cSTATE_TRAVERSE);
      return;
    }

    // Build anchor
    C2D coord = ew.getCoordRaw(getForwardSite());
    coord = cu.rotateLeft(coord);
    SiteNum site = ew.getSiteNumberRaw(-coord);
    if (!ew.isEmpty(site))
      return;
    Anchor anchor;
    ew[site] = anchor;

    // Attach to membrane
    BS.Status status = bu.attach(site, 0, membraneSite, Membrane.cOTHER);
    if (!bs.isOk(status)) {
      Empty empty;
      ew[site] = empty;
      return;
    }
  }

  Void traverse() {
    BondStatus bs;
    BondUtils bu;
    EventWindow ew;

    BS.Status status = bu.traverse(0, 0, Membrane.cNEXT);
    if (bs.isOk(status) || status == BS.cNO_NEXT)
      setState(cSTATE_FORWARD);
  }

  Void forward() {
    // NOTE: cannot do checks after swapping
    if (mMoveCount >= cSTEP_LENGTH) {
      mMoveCount = 0;
      mCount++;
      if (mCount >= cSIDE_LENGTH) {
        // Side finished, move to next side
        mCount = 0;
        mDir = getNextDir();
        if (mDir == 0) {
          // Last side finished, close the membrane
          setState(cSTATE_CLOSE);
          return;
        }
      }
      setState(cSTATE_BUILD);
      return;
    }

    EventWindow ew;
    SwapHelper sh;

    SiteNum forwardSite = getForwardSite();
    if (ew.isEmpty(forwardSite)) {
      mMoveCount++; // increment _before_ swapping
      if (!sh.swap(0, forwardSite))
        mMoveCount--;
    }
  }

  Void close() {
    BondStatus bs;
    BondUtils bu;
    EventWindow ew;

    QBond& bond = getBond(0);
    SiteNum prevSite = bond.getSiteNumber();
    SiteNum nextSite = getFirstSite();
    if (nextSite == SiteNum.maxof)
      return;

    BS.Status status = bu.attach(prevSite, Membrane.cNEXT, nextSite, Membrane.cPREV);
    if (bs.isOk(status)) {
      mCount = 0;
      setState(cSTATE_REMOVE_ANCHORS);
    }
  }

  Void removeAnchors() {
    BondUtils bu;
    EventWindow ew;

    // Get membrane
    QBond& bond = getBond(0);
    SiteNum membraneSite = bond.getSiteNumber();
    Membrane& membrane = (Membrane&) ew[membraneSite];

    // Get anchor
    QBond& anchorBond = membrane.getBond(Membrane.cOTHER);
    Bool isAttached = anchorBond.isAttached();
    SiteNum anchorSite = isAttached
      ? anchorBond.getSiteNumber(membraneSite)
      : SiteNum.maxof;

    if (isAttached && !ew.isAccessible(anchorSite))
      return;

    if (!isAttached || !(ew[anchorSite] is Anchor)) {
      // no anchor, move to next membrane atom
      bu.traverse(0, 0, Membrane.cNEXT);
      return;
    }

    // Detach and destroy anchor
    anchorBond.detach(membraneSite);
    Empty empty;
    ew[anchorSite] = empty;
    mCount++;

    // Are all anchors removed?
    if (mCount == 4)
      setState(cSTATE_DONE);
  }

  SiteNum getFirstSite() {
    EventWindow ew;
    MDist mdist;

    for (Int s = 1; s <= mdist.getLastIndex(4); s++) {
      SiteNum site = (SiteNum) s;
      Atom& atom = ew[site];
      if (atom as Membrane)
        if (!atom.getBond(Membrane.cPREV).isAttached())
          return site;
    }
    return SiteNum.maxof;
  }

  Dir getNextDir() {
    return (Dir) (((Int) mDir + 2) % 8);
  }

  SiteNum getForwardSite() {
    EventWindow ew;
    C2D origin;
    return ew.getSiteNumber(origin.neighbor(mDir));
  }

  @Override Void renderGraphics() {
    self.QBondable1.renderGraphics();

    Drawable drawable;
    if (!drawable.canDraw())
      return;

    ColorUtils cu;
    C2D start;
    C2D end = start.neighbor(mDir) * (Drawable.cUNITS_PER_SITE * 3 / 4);
    ARGB oncol = cu.color(0x00ffffff);
    ARGB offcol = oncol;
    drawable.drawMaskedLine(start, end, oncol, offcol, 0xffffffff);
  }
}


/**
   Basic crawler example:
   (with-input
     (if-attached
       (widh-dst           ;; Move to "next" if possible, otherwise try "other"
         Membrane.cNEXT,
         (if-attached-next
           (traverse-3)
           (traverse-2)))
       (attach-to          ;; Find a membrane to attach to
         STU.cMEMBRANE)))

   # Build other layer atoms:
    1 (with-input
    2  (if-attached
    3    (with-dst
    4      Membrane.cOTHER              ; "other" membrane bond
    5      (if-attached-next
    6        (with-dst                  ;; Move to "next" if attached, otherwise try "other"
    7          Membrane.cNEXT
    8          (if-attached-next
    9            (traverse-3)           ; move to "next" membrane atom
   10            (traverse-2)))         ; move to "other" membrane atom
   11        (with-output               ;; Attach new membrane atom to "other" bond
   12          (prog2
   13            (build                 ; build new membrane atom
   14              STU.cMEMBRANE        ; membrane stype
   15              Membrane.cOTHER)     ; "other" bond
   16            (move-to-other
   17              Membrane.cOTHER))))) ; attach to input membrane's "other" bond
   18    (attach-to                     ;; Find a membrane to attach to
   19      STU.cMEMBRANE)))


   # Repair the membrane:
   1. Next layer atoms are there,        2. Atoms of both layers are attached,
      but not attached to each other:       but one layer has extra atoms in between
                                            because of growth:

   "other" layer ->   o   o                    -o-------o-
                      |   |                     |       |
   current layer ->  -o---o-                   -o---o---o-
                      |                         |
                      @                         @
   20 (with-input
   21   (if-attached
   22   +-(prog2
   23   |   (with-dst
   24   |     Membrane.cOTHER
   25   |     (if-attached-next                        ; Does current membrane atom have "other"?
   26   |     +-(if
   27   |     | +-(attach-other-to-next)               ; * attach output bond to "other" membrane
   28   |     | +-(with-dst
   29   |     |     Membrane.cNEXT
   30   |     |     (if-attached-next                  ;   Is there next membrane atom?
   31   |     |     +-(prog2
   32   |     |     |   (traverse)                     ;   - move to next
   33   |     |     |   (with-dst
   34   |     |     |     Membrane.cOTHER
   35   |     |     |     (if-attached-next            ;     does next atom have "other" attached?
   36   |     |     |     +-(prog2
   37   |     |     |     |   (traverse)               ;     1. move to it
   38   |     |     |     |   (with-dst
   39   |     |     |     |     Membrane.cPREV
   40   |     |     |     |     (if-other-matches-next ;     2. is it "next" for output?
   41   |     |     |     |       (noop)               ;        nothing to do then
   42   |     |     |     |       (connect-other-force ;     3. connect membrane prev/next atoms
   43   |     |     |     |         Membrane.cNEXT)))) ;        (break bonds if already bonded)
   44   |     |     |     +-(noop))))
   45   |     |     +-(swap-input-output))))           ;   - no next atom, move to "other" by swapping
   46   |     +-(traverse-3))                          ; * move to next
   47   |   (detach-other))                            ; detach output bond
   48   +-(attach-to                                   ;; Find a membrane to attach to
   49       STU.cMEMBRANE)))
 */
transient Demo_SequenceData {
  typedef DataUtils.Datum Datum;
  typedef DataUtils.Byte Byte;
  typedef ITaggable.Tag Tag;
  typedef PackageData.Arity Arity;
  typedef PackageData.Type Type;
  typedef PackageData.Symbol Symbol;

  typedef EvalBlock EB;
  typedef EvalConditional EC;
  typedef EvalFunction EF;

  typedef Bits(Type.sizeof + Byte.sizeof) Item ;

  constant Item cBLCK = (Item) PD.cTYPE_BLOCK << Byte.sizeof;
  constant Item cFUNC = (Item) PD.cTYPE_FUNCTION << Byte.sizeof;
  constant Item cCOND = (Item) PD.cTYPE_CONDITIONAL << Byte.sizeof;
  constant Item cCNST = (Item) PD.cTYPE_CONSTANT << Byte.sizeof;

  constant Item cACTIVE = (Item) 1 << Byte.sizeof;

  constant Unsigned cSIZE = 50;

  PackageData pd;

  EvalBlock eb;
  EvalConditional ec;
  EvalFunction ef;

  Item items[cSIZE * 2] = {
    /* Growind second layer of membrane */
    /*  1 */ cBLCK | EB.cWITH_INPUT, cACTIVE | (Tag) 0x1,
    /*  2   */ cCOND | EC.cIF_ATTACHED, 0,
    /*  3     */ cBLCK | EB.cWITH_DST, 0,
    /*  4       */ cCNST | (Byte) Membrane.cOTHER, 0,
    /*  5       */ cCOND | EC.cIF_ATTACHED_NEXT, 0,
    /*  6         */ cBLCK | EB.cWITH_DST, 0,
    /*  7           */ cCNST | (Byte) Membrane.cNEXT, 0,
    /*  8           */ cCOND | EC.cIF_ATTACHED_NEXT, 0,
    /*  9             */ cFUNC | EF.cTRAVERSE_3, 0,
    /* 10             */ cFUNC | EF.cTRAVERSE_2, 0,
    /* 11         */ cBLCK | EB.cWITH_OUTPUT, 0,
    /* 12            */ cFUNC | EF.cPROG2, 0,
    /* 13              */ cFUNC | EF.cBUILD, 0,
    /* 14                */ cCNST | (Byte) STU.cMEMBRANE, 0,
    /* 15                */ cCNST | (Byte) Membrane.cOTHER, 0,
    /* 16              */ cFUNC | EF.cMOVE_TO_OTHER, 0,
    /* 17                */ cCNST | (Byte) Membrane.cOTHER, 0,
    /* 18     */ cFUNC | EF.cATTACH_TO, 0,
    /* 19       */ cCNST | STU.cMEMBRANE, 0,
    /* Repairing the membrane */
    /* 20 */ cBLCK | EB.cWITH_INPUT, cACTIVE | (Tag) 0x1,
    /* 21   */ cCOND | EC.cIF_ATTACHED, 0,
    /* 22     */ cFUNC | EF.cPROG2, 0,
    /* 23       */ cBLCK | EB.cWITH_DST, 0,
    /* 24         */ cCNST | (Byte) Membrane.cOTHER, 0,
    /* 25         */ cCOND | EC.cIF_ATTACHED_NEXT, 0,
    /* 26           */ cCOND | EC.cIF, 0,
    /* 27             */ cFUNC | EF.cATTACH_OTHER_TO_NEXT, 0,
    /* 28             */ cBLCK | EB.cWITH_DST, 0,
    /* 29               */ cCNST | (Byte) Membrane.cNEXT, 0,
    /* 30               */ cCOND | EC.cIF_ATTACHED_NEXT, 0,
    /* 31                 */ cFUNC | EF.cPROG2, 0,
    /* 32                   */ cFUNC | EF.cTRAVERSE, 0,
    /* 33                   */ cBLCK | EB.cWITH_DST, 0,
    /* 34                     */ cCNST | (Byte) Membrane.cOTHER, 0,
    /* 35                     */ cCOND | EC.cIF_ATTACHED_NEXT, 0,
    /* 36                       */ cFUNC | EF.cPROG2, 0,
    /* 37                         */ cFUNC | EF.cTRAVERSE, 0,
    /* 38                         */ cBLCK | EB.cWITH_DST, 0,
    /* 39                           */ cCNST | (Byte) Membrane.cPREV, 0,
    /* 40                           */ cCOND | EC.cIF_OTHER_MATCHES_NEXT, 0,
    /* 41                             */ cFUNC | EF.cNOOP, 0,
    /* 42                             */ cFUNC | EF.cCONNECT_OTHER_FORCE, 0,
    /* 43                               */ cCNST | (Byte) Membrane.cNEXT, 0,
    /* 44                       */ cFUNC | EF.cNOOP, 0,
    /* 45                 */ cFUNC | EF.cSWAP_INPUT_OUTPUT, 0,
    /* 46           */ cFUNC | EF.cTRAVERSE_3, 0,
    /* 47       */ cFUNC | EF.cDETACH_OTHER, 0,
    /* 48   */ cFUNC | EF.cATTACH_TO, 0,
    /* 49     */ cCNST | (Byte) STU.cMEMBRANE, 0
  };

  Datum getData(Unsigned i) {
    Item item = items[i * 2u];

    Type type = (Type) (item >> Byte.sizeof);
    Byte value = (Byte) item;
    Arity arity = getArity(type, (Symbol) value);

    // DebugUtils dbg;
    // dbg.print("sequence item (num/arity):");
    // dbg.print(i + 1u);
    // dbg.print(arity);

    Datum data = PD.cNO_DATA;
    data = pd.setArity(data, arity);
    data = pd.setType(data, type);
    data = pd.setValue(data, value);
    return data;
  }

  Datum getAux(Unsigned i) {
    Item item = items[i * 2u + 1u];

    Tag tag = (Tag) item;
    Bool isActive = (item & cACTIVE) != 0x0;

    Datum data = PD.cNO_DATA;
    if (tag != 0x0) {
      data = pd.setTag(data, tag);
      data = pd.setIsActive(data, isActive);
    }
    return data;
  }

  Arity getArity(Type type, Symbol symbol) {
    Arity arity = 0;
    if (type == PD.cTYPE_BLOCK) {
      arity = eb.getArity(symbol);
    } else if (type == PD.cTYPE_CONDITIONAL) {
      arity = ec.getArity(symbol);
    } else if (type == PD.cTYPE_FUNCTION) {
      arity = ef.getArity(symbol);
    }
    return arity;
  }
}
