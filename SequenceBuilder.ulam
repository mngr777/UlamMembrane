local typedef BondStatus BS;
local typedef EventWindow.SiteNum SiteNum;

transient SequenceBuilder + Fail {
  typedef QBond.Index Index;
  typedef DataUtils.Datum Datum;

  BondStatus bs;
  BondUtils bu;
  EventWindow ew;
  EventWindowMisc ewm;

  Bool buildNext(Atom& builder, Index index, Datum data, Datum aux) {
    return buildNext(builder, index, data, aux, false);
  }

  Bool buildNext(Atom& builder, Index index, Datum data, Datum aux, Bool reverse) {
    if (!(builder is IBondable))
      fail("Builder atom is not bondable");

    QBond.Index prevIndex = reverse ? Package.cNEXT : Package.cPREV;
    QBond.Index nextIndex = reverse ? Package.cPREV : Package.cNEXT;

    IBondable& bondable = (IBondable&) builder;
    QBond& bond = bondable.getBond(index);
    SiteNum site = ew.getSiteNumber(builder);

    // Build
    Package pkg;
    pkg.setData(data);
    pkg.setAux(aux);
    SiteNum nextSite = ewm.findEmptySiteAround(builder, 1u, bu.maxDist(bondable, pkg));
    if (nextSite == SiteNum.maxof)
      return false;
    ew[nextSite] = pkg;
    Package& next = (Package&) ew[nextSite];

    BS.Status status = BS.cOK;

    if (bond.isAttached()) {
      // Attach to previous item
      QBond& prevBond = next.getBond(prevIndex);
      status = prevBond.attach(nextSite, prevIndex, bond.getSiteNumber(), nextIndex);
      if (!bs.isOk(status)) {
        Empty empty;
        ew[nextSite] = empty;
        return false;
      }

      // Move builder to next item
      status = bu.traverse(site, index, nextIndex);
      if (!bs.isOk(status))
        fail("SequenceBuilder.buildNext: failed to traverse to next item");

    } else {
      // First item, bond to the builder
      status = bond.attach(site, index, nextSite, Package.cCOMMON);
      if (!bs.isOk(status)) {
        Empty empty;
        ew[nextSite] = empty;
        return false;
      }
    }
    return true;
  }
}
