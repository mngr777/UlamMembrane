local typedef EvalStatefulFunction ESF;

transient EvalFunction : Eval {
  constant Symbol cSET_1                = 0x01;
  constant Symbol cSET_2                = 0x02;
  constant Symbol cSET_SRC              = 0x03;
  constant Symbol cSET_DST              = 0x04;
  constant Symbol cGET_1                = 0x05;
  constant Symbol cGET_2                = 0x06;
  constant Symbol cGET_SRC              = 0x07;
  constant Symbol cGET_DST              = 0x08;

  constant Symbol cATTACH_TO            = 0x11;
  constant Symbol cDETACH               = 0x12;

  constant Symbol cTRAVERSE             = 0x13;
  constant Symbol cTRAVERSE_1           = 0x14;
  constant Symbol cTRAVERSE_2           = 0x15;
  constant Symbol cTRAVERSE_3           = 0x16;

  constant Symbol cFIND_TYPE            = 0x21;
  constant Symbol cFIND_TYPE_RADIUS     = 0x22;

  Arity getArity(Symbol symbol) {
    return getArgNum(symbol);
  }

  Arity getArgNum(Symbol symbol) {
    if (symbol == cSET_1 ||
        symbol == cSET_2 ||
        symbol == cSET_SRC ||
        symbol == cSET_DST ||
        symbol == cATTACH_TO ||
        symbol == cTRAVERSE)
    {
      return 1;
    }
    if (symbol == cFIND_TYPE_RADIUS) {
      return 2;
    }
    return 0;
  }

  Byte eval(Exec& exec, Symbol symbol, Byte arg1, Byte arg2) {
    if (symbol == cSET_1) {
      if (cDEBUG) dbg.print("(set-1)");
      exec.set(cREG1, arg1);

    } else if (symbol == cSET_2) {
      if (cDEBUG) dbg.print("(set-2)");
      exec.set(cREG2, arg1);

    } else if (symbol == cSET_SRC) {
      if (cDEBUG) dbg.print("(set-src)");
      exec.set(cSRC, arg1);

    } else if (symbol == cSET_DST) {
      if (cDEBUG) dbg.print("(set-dst)");
      exec.set(cDST, arg1);

    } else if (symbol == cGET_1) {
      if (cDEBUG) dbg.print("(get-1)");
      return exec.get(cREG1);

    } else if (symbol == cGET_2) {
      if (cDEBUG) dbg.print("(get-2)");
      return exec.get(cREG2);

    } else if (symbol == cGET_SRC) {
      if (cDEBUG) dbg.print("(get-src)");
      return exec.get(cSRC);

    } else if (symbol == cGET_DST) {
      if (cDEBUG) dbg.print("(get-dst)");
      return exec.get(cDST);


    } else if (symbol == cATTACH_TO) {
      if (cDEBUG) dbg.print("(attach-to)");
      return (Byte) attach(exec, toSiteNumber(arg1));

    } else if (symbol == cDETACH) {
      if (cDEBUG) dbg.print("(detach)");
      detach(exec);


    } else if (symbol == cTRAVERSE) {
      if (cDEBUG) dbg.print("(traverse)");
      traverse(exec, toBondIndex(arg1));

    } else if (symbol == cTRAVERSE_1) {
      if (cDEBUG) dbg.print("(traverse-1)");
      traverse(exec, 1);

    } else if (symbol == cTRAVERSE_2) {
      if (cDEBUG) dbg.print("(traverse-2)");
      traverse(exec, 2);

    } else if (symbol == cTRAVERSE_3) {
      if (cDEBUG) dbg.print("(traverse-3)");
      traverse(exec, 3);


    } else if (symbol == cFIND_TYPE) {
      if (cDEBUG) dbg.print("(find-type)");
      return (Byte) findType(exec, (SType) arg1);

    } else if (symbol == cFIND_TYPE_RADIUS) {
      if (cDEBUG) dbg.print("(find-type-radius)");
      return (Byte) findTypeRadius(exec, (SType) arg1, (Radius) arg2);
    }

    return 0x0;
  }


  Bool attach(Exec& exec, SiteNum siteOther) {
    Index index = getBondIndex(exec, cSRC);
    return bs.isOk(bu.attach(0, index, siteOther, 0));
  }

  Void detach(Exec& exec) {
    Index index = getBondIndex(exec, cSRC);
    bu.detach(0, index);
  }

  Void traverse(Exec& exec, Index indexNext) {
    exec.setInteger(cDST, indexNext);
    exec.setState(ESF.cSTATE_TRAVERSE);
  }

  SiteNum findType(Exec& exec, SType type) {
    return findTypeRadius(exec, type, 4u);
  }

  SiteNum findTypeRadius(Exec& exec, SType type, Radius radius) {
    WindowServices ws;
    ws.reset(1u, radius);
    for (Int s = ws.next(); s >= 0; s = ws.next()) {
      if (stu.getSType((SiteNum) s) == type)
        ws.hit();
    }
    SiteNum pick = ws.getPick();
    return (pick != SiteNum.maxof) ? pick : 0u;
  }
}
