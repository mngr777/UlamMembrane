local typedef EvalStatefulFunction ESF;

transient EvalFunction : Eval {
  constant Symbol cSET_1                = 0x01;
  constant Symbol cSET_2                = 0x02;
  constant Symbol cSET_SRC              = 0x03;
  constant Symbol cSET_DST              = 0x04;
  constant Symbol cGET_1                = 0x05;
  constant Symbol cGET_2                = 0x06;
  constant Symbol cGET_SRC              = 0x07;
  constant Symbol cGET_DST              = 0x08;

  constant Symbol cPROG2                = 0x11;

  constant Symbol cATTACH_TO            = 0x21;
  constant Symbol cDETACH               = 0x22;

  constant Symbol cTRAVERSE             = 0x23;
  constant Symbol cTRAVERSE_1           = 0x24;
  constant Symbol cTRAVERSE_2           = 0x25;
  constant Symbol cTRAVERSE_3           = 0x26;

  constant Symbol cBUILD                = 0x27;
  constant Symbol cMOVE_TO_OTHER        = 0x28;

  Arity getArity(Symbol symbol) {
    return getArgNum(symbol);
  }

  Arity getArgNum(Symbol symbol) {
    if (symbol == cSET_1 ||
        symbol == cSET_2 ||
        symbol == cSET_SRC ||
        symbol == cSET_DST ||
        symbol == cATTACH_TO ||
        symbol == cTRAVERSE ||
        symbol == cMOVE_TO_OTHER)
    {
      return 1;
    }

    if (symbol == cPROG2 ||
        symbol == cBUILD)
    {
      return 2;
    }

    return 0;
  }

  Byte eval(Exec& exec, Symbol symbol, Byte arg1, Byte arg2) {
    if (symbol == cSET_1) {
      if (cDEBUG) dbg.print("(set-1)");
      exec.set(cREG1, arg1);

    } else if (symbol == cSET_2) {
      if (cDEBUG) dbg.print("(set-2)");
      exec.set(cREG2, arg1);

    } else if (symbol == cSET_SRC) {
      if (cDEBUG) dbg.print("(set-src)");
      exec.set(cSRC, arg1);

    } else if (symbol == cSET_DST) {
      if (cDEBUG) dbg.print("(set-dst)");
      exec.set(cDST, arg1);

    } else if (symbol == cGET_1) {
      if (cDEBUG) dbg.print("(get-1)");
      return exec.get(cREG1);

    } else if (symbol == cGET_2) {
      if (cDEBUG) dbg.print("(get-2)");
      return exec.get(cREG2);

    } else if (symbol == cGET_SRC) {
      if (cDEBUG) dbg.print("(get-src)");
      return exec.get(cSRC);

    } else if (symbol == cGET_DST) {
      if (cDEBUG) dbg.print("(get-dst)");
      return exec.get(cDST);

    } else if (symbol == cPROG2) {
      // no debug output
      return arg2;

    } else if (symbol == cATTACH_TO) {
      if (cDEBUG) dbg.print("(attach-to)");
      return (Byte) attach(exec, findSType(exec, (SType) arg1));

    } else if (symbol == cDETACH) {
      if (cDEBUG) dbg.print("(detach)");
      detach(exec);


    } else if (symbol == cTRAVERSE) {
      if (cDEBUG) dbg.print("(traverse)");
      traverse(exec, toBondIndex(arg1));

    } else if (symbol == cTRAVERSE_1) {
      if (cDEBUG) dbg.print("(traverse-1)");
      traverse(exec, 1);

    } else if (symbol == cTRAVERSE_2) {
      if (cDEBUG) dbg.print("(traverse-2)");
      traverse(exec, 2);

    } else if (symbol == cTRAVERSE_3) {
      if (cDEBUG) dbg.print("(traverse-3)");
      traverse(exec, 3);

    } else if (symbol == cBUILD) {
      if (cDEBUG) dbg.print("(build)");
      exec.set(cREG1, arg1); // stype
      exec.set(cDST, arg2);  // new atom bond to attach to
      exec.setState(ESF.cSTATE_BUILD);

    } else if (symbol == cMOVE_TO_OTHER) {
      if (cDEBUG) dbg.print("(move-to-other)");
      // Move output atom to input or vice versa
      exec.set(cDST, arg1); // other atom bond index
      exec.setState(ESF.cSTATE_MOVE_TO_OTHER);

    } else {
      dbg.print("EvalFunction.eval: Unknown symbol:");
      dbg.print((Unsigned) symbol);
    }

    return 0x0;
  }


  Bool attach(Exec& exec, SiteNum siteOther) {
    if (siteOther == 0u)
      return false;
    Index index = getBondIndex(exec, cSRC);
    return bs.isOk(bu.attach(0, index, siteOther, 0));
  }

  Void detach(Exec& exec) {
    Index index = getBondIndex(exec, cSRC);
    bu.detach(0, index);
  }

  Void traverse(Exec& exec, Index indexNext) {
    exec.setInteger(cDST, indexNext);
    exec.setState(ESF.cSTATE_TRAVERSE);
  }

  SiteNum findSType(Exec& exec, SType type) {
    Atom atom = stu.make(type);
    return findSTypeRadius(exec, type, bu.maxDist((IBondable&) atom, exec));
  }

  SiteNum findSTypeRadius(Exec& exec, SType type, Radius radius) {
    WindowServices ws;
    ws.reset(1u, radius);
    for (Int s = ws.next(); s >= 0; s = ws.next()) {
      if (stu.getSType((SiteNum) s) == type)
        ws.hit();
    }
    SiteNum pick = ws.getPick();
    return (pick != SiteNum.maxof) ? pick : 0u;
  }
}
